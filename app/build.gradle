apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.decomp.comp.decomp"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 17
        versionName "v2.0.0-${getGitHash()}"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        debug{
            resValue "string", "support_dev_ad_unit_id", "ca-app-pub-3940256099942544/5224354917"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "support_dev_ad_unit_id", "ca-app-pub-4200151682628675/7937682345"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            //changes the generated .apk name
            if (variant.name.contains("release")) {
                def formattedDate = new Date().format('dd.MM.yy_HHmm')
                outputFileName = "decomp_${variant.name}_${variant.versionCode}_${formattedDate}_${getGitHash()}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.core:core:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha04'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.google.firebase:firebase-core:17.4.3'
    implementation 'com.google.firebase:firebase-ads:19.2.0'
    implementation 'com.airbnb.android:lottie:3.3.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    //crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    //image picker
    implementation 'com.github.nguyenhoanglam:ImagePicker:1.4.3'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Required for all integration of Flurry, includes Analytics
    //compile 'com.flurry.android:analytics:6.4.2'
    // Optional - If you want to use the Ads SDK
    //compile 'com.flurry.android:ads:6.4.2'
}
apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
