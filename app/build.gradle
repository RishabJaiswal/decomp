apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.decomp.comp.decomp"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 11
        versionName "v1.1.1-${getGitHash()}"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            //changes the generated .apk name
            if (variant.name.contains("release")) {
                def formattedDate = new Date().format('dd.MM.yy_HHmm')
                outputFileName = "decomp_${variant.name}_${variant.versionCode}_${formattedDate}_${getGitHash()}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.core:core:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'com.github.darsh2:MultipleImageSelect:v0.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha06'
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-ads:17.2.1'
    implementation 'com.airbnb.android:lottie:2.8.0'
    //crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    // Required for all integration of Flurry, includes Analytics
    //compile 'com.flurry.android:analytics:6.4.2'
    // Optional - If you want to use the Ads SDK
    //compile 'com.flurry.android:ads:6.4.2'
}
apply plugin: 'com.google.gms.google-services'
